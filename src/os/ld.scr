OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
    ram(rwx)    : o = 0xffc020, l = 0x003f00
    stack(rw)   : o = 0xffff00, l = 0x000000 /* end of RAM */
}

SECTIONS
{
    /* CPU が実行する機械語コード */
    .text : {
        _text_start = . ;
        *(.text)
        _etext = . ;
    } > ram

    /* const 定義した変数や文字列リテラルなど */
    .rodata : {
        _rodata_start = . ;
        *(.strings)
        *(.rodata)
        *(.rodata.*)
        _erodata = . ;
    } > ram

    /* 初期値を持つ静的変数など */
    .data : {
        _data_start = . ;
        *(.data)
        _edata = . ;
    } > ram

    /* 初期値を持たない静的変数など */
    .bss : {
        _bss_start = . ;
        *(.bss)
        *(COMMON)
        _ebss = . ;
    } > ram

    . = ALIGN(4);
    _end = . ;

    .stack : {
        _stack = . ;
    } > stack
}
